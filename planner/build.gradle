plugins {
    id 'com.google.protobuf' version '0.8.14'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.aiddl.example'
version = '0.1'

mainClassName = "org.aiddl.example.maze_planner.planner.Run"

repositories {
    jcenter()
    mavenLocal()
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'            
        }
   }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    
    implementation 'io.grpc:grpc-netty-shaded:1.35.0'
    implementation 'io.grpc:grpc-protobuf:1.35.0'
    implementation 'io.grpc:grpc-stub:1.35.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

    implementation("org.aiddl:aiddl-core-java") {
        version {
            strictly("[2.0.0, 3.0.0[")
        }
    }

    implementation("org.aiddl:aiddl-util-java") {
        version {
            strictly("[1.0.0, 2.0.0[")
        }
    }
    
    implementation("org.aiddl:aiddl-common-java") {
        version {
            strictly("[2.0.0, 3.0.0[")
        }
    }
}

run {
    main = "org.aiddl.example.maze_planner.planner.Run"
    classpath = sourceSets.main.runtimeClasspath 
    
    minHeapSize = "256m"
    maxHeapSize = "4096m"
    
    jvmArgs '-noverify'

    systemProperties System.getProperties()

    if(System.getProperty("exec.args") != null) {
      args System.getProperty("exec.args").split()
    }
}



protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.12.0"
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.35.0'
    }
  }
  //generatedFilesBaseDir = 'src'
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}